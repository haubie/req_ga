<!-- livebook:{"file_entries":[{"file":{"file_system_id":"local","file_system_type":"local","path":"/Users/haubie/Development/livebook/credentials.json"},"name":"credentials.json","type":"file"}]} -->

# ReqGA demonstration

```elixir
Mix.install(
  [
    {:req_ga, "~> 0.1"},
    {:kino_db, "~> 0.2"},
    {:goth, "~> 1.3"},
    {:req, "~> 0.4"},
    :explorer,
    {:kino_vega_lite, "~> 0.1"}
  ]
)
```

## Introduction

### Context

Since the retirement of the previous version of Google Analytics (known as Universal Analytics), Google is [actively developing APIs](https://developers.google.com/analytics) for it's replacement, [Google Analytics 4](https://developers.google.com/analytics/devguides/collection/ga4) (abbreviated to GA4 in this notebook).

As of 2023, most of these APIs are in either *alpha* or *beta* releases and are subject to refinement.

### GA4 APIs

There are a number of APIs for interacting with GA4:

* [**Data API**](https://developers.google.com/analytics/devguides/reporting/data): used to generate GA4 report data
* [**Admin API**](https://developers.google.com/analytics/devguides/config/admin): used to access, create and modify GA4 configuration data
* [**User Deletion API**](https://developers.google.com/analytics/devguides/config/userdeletion/): use to delete data for a given user from a project or web property (e.g. for safeguarding data or for privacy reasons).

### Req and ReqGA

[Req](https://github.com/wojtekmach/req) is an innovative and flexible HTTP client by [Wojtek Mach](https://github.com/wojtekmach). It's flexibility is due to the request and response cycle being chunked into a series of steps which can be re-used, re-arranged or even added to.

The ReqGA plugin library does this, adding additional steps to make it easier interacting with GA4 HTTP API endpoints as well as decoding the data returned so it is easier to work with from Elixir.

Many of the ReqGA's structs implement the [`Table.Reader`](https://hex.pm/packages/table) protocol which makes it play nicely with other `Table.Reader` compatible libraries and tools, such as [Livebook](https://livebook.dev/), various [Kino](https://github.com/livebook-dev/kino) widgets and the DataFrame library [Explorer](https://github.com/elixir-explorer/explorer).

### Uses of ReqGA

The uses of ReqGA are only limited by what the GA4 APIs allow and your imagination! Some example uses may be:

* Exploratory data analysis within Livebook using Explorer and Kino
* Custom Phoenix LiveView reporting dashboards
* Automating batch reports, for example, emailing out a monthly report
* Reacting to realtime GA4 events
* Implementing custom data pipelines using GA4 data, such as an ETL or data value-adding workload
* Simplifying or automating administration tasks, such as bulk configuration of custom dimensions and metrics, etc.

### What APIs methods been implemented - *so far!*

The table below summarises what GA4 API endpoints have been implemented so far:

| :ga API method           | API       | Endpoint                | Req HTTP method supported |
| ------------------------ | --------- | ----------------------- | ------------------------- |
| :run_report              | Data API  | ":runReport"            | post                      |
| :batch_run_reports       | Data API  | ":batchRunReports"      | post                      |
| :run_pivot_report        | Data API  | ":runPivotReport"       | post                      |
| :batch_run_pivot_reports | Data API  | ":batchRunPivotReports" | post                      |
| :run_realtime_report     | Data API  | ":runRealtimeReport"    | post                      |
| :check_compatibility     | Data API  | ":checkCompatibility"   | post                      |
| :metadata                | Data API  | "/metadata"             | get                       |
| :audience_lists          | Data API  | "/audienceLists"        | get                       |
| :account_summaries       | Admin API | "/accountSummaries"     | get                       |
| :custom_dimensions       | Admin API | "/customDimensions"     | get, post                 |
| :custom_metrics          | Admin API | "/customMetrics"        | get, post                 |
| :accounts                | Admin API | "/accounts"             | get, delete, post         |
| :properties              | Admin API | "/accounts"             | get, delete, post         |

Some endpoints are are quite simple and you'll use `Req.get` or `Req.get!` with the required parameters. Others will require a json payload where you'll use `Req.post` or `Req.post!`.

If in doubt, refer to the Google's official API documentation for more information on which to use. Google's documentation also includes the structure of the payload and expected values.

#### Example code snippet

Assuming [Goth](https://hex.pm/packages/goth) has been started (used to authenticate to Google Cloud), and the ReqGA library has been attached to Req, e.g.:

```
iex> req = Req.new() |> ReqGA.attach(goth: GA)
```

You can get a list of the custom dimensions for a GA4 property as follows:

```
iex> property_id = "properties/264264328"
iex> res = Req.get!(req, ga: :custom_dimensions, property_id: property_id)
iex> res.body # the body contains the data returned from the GA4 API.
```

### General steps

The steps when using this library are:

1. **Authenticate to Google Cloud with Goth.** This will likely require a [service account](https://developers.google.com/analytics/devguides/reporting/data/v1/quickstart-client-libraries) created in the Google Cloud Console, and adding it into the Google Analytics Account(s) or Properties you wish to interact with.
   Different API calls require different levels of permissions, set though authorisation scopes.
2. **Attach ReqGA to Req**. The active Goth process is attached at this point.
3. **Interact the APIs via Req**. The attached ReqGA is passed to Req with each `Req.get!` or `Req.post!` call, ensuring as an ergonmic as experience as possible. An atom representing the API call you wish to make is passed using the `ga:` parameter, e.g. `Req.get!(req, ga: :account_summaries).body` which is equivalent of calling the "/accountSummaries" endpoint on the GA4 Admin API.
4. **The data is in Req's response**. If sucessfull the `:body` of Req's response will have the result of your call.

*That's it!*

### Get ready...

The examples below will take you though these steps, starting with authenticaing to Google Cloud with Goth. For these examples to function you will need to have:

* your service account setup with the service's account `credentials.json` file added to Livebooks file's section (see the file icon to the left of this page).
* one or more valid GA4 property IDs so you can interact with the property.

## Authenticate to Google Cloud

Load credentials.json file, set Google API scopes and use Goth to authenticate to Google Cloud.

```elixir
credentials = "credentials.json" |> File.read!() |> Jason.decode!()

scopes = [
  "https://www.googleapis.com/auth/analytics",
  "https://www.googleapis.com/auth/analytics.edit",
  "https://www.googleapis.com/auth/analytics.readonly",
  "https://www.googleapis.com/auth/analytics.manage.users",
  "https://www.googleapis.com/auth/analytics.manage.users.readonly"
]

source = {:service_account, credentials, [scopes: scopes]}
{:ok, _} = Goth.start_link(name: GA, source: source, http_client: &Req.request/1)
```

## Use Req GA4 plugin to query Google Analytics

Attach ReqGA to Req's request and response steps:

```elixir
req = Req.new() |> ReqGA.attach(goth: GA)
```

### Run a report

<!-- livebook:{"break_markdown":true} -->

Below is an example of a simple report. For each **country** it asks for the number of:

* active users
* user engagement duration (in seconds)
* engagement rate (a float representing the percentage); and the
* organic Google Search click-through rate (also a float).
  It does this for the date range **"2023-09-01" to "2023-09-30"**.

Feel free to:

* adjust the date range it isn't valid for your GA4 property
* change the `property_id` to the ID of a propery you have access to.

#### A note on property IDs

Property IDs are in the format `"properties/<id number>"`, for example:

```
iex> property_id = "properties/264264328"
```

When this library looks for a property ID, make sure the `"properties/"` is prepended to the number as above.

#### A note on json payloads

Note that some of the GA4 APIs require you to post a json payload with the actions and data required for that endpoint. `:run_report` is one of those.

Because Req automatically converts Elixir types into a valid json payload, you can write your report request using Elixir datatypes as below.

#### Scopes

`:run_report` requires one of the following OAuth scopes:

* https://www.googleapis.com/auth/analytics.readonly
* https://www.googleapis.com/auth/analytics

```elixir
# ID of property to query. In the format "properties/<id number>"
property_id = "properties/453770299"

# Define a report to be posted
report = %{
  "dateRanges" => [%{"startDate" => "2024-10-01", "endDate" => "2024-10-28"}],
  "dimensions" => [%{"name" => "country"}],
  "metrics" => [
    %{"name" => "activeUsers"},
    %{"name" => "userEngagementDuration"},
    %{"name" => "engagementRate"},
    %{"name" => "organicGoogleSearchClickThroughRate"}
  ]
}
```

Now we've defined our report above, lets post to the `:run_report` endpoint, passing the `:property_id` parameter and `:json` report payload as below:

```elixir
# Run the report with the :run_report method
res = Req.post!(req, ga: :run_report, property_id: property_id, json: report)
```

If successful (`status: 200` in the header) the response `:body` will hold the data returned from the Google Analytics API end point.

If unsuccessfull (e.g. a `status: 404` in the header) the body will hold HTML containing information on the error. For example, the permissions may not have been set in Google Cloud Console.

Lets look at the response `:body` below:

```elixir
country_data = res.body
```

You can see a populated `%ReqGA.ReportResponse{}` struct, containing the report data. Some of the struct's fields are blank as they're not relevant to the report query we sent.

### Dimensions and metrics

The `:dimensions` and `:metrics` key holds information about what dimensions and metrics are included in the report. The metrics also include type information in the tuple, for example `"activeUsers"` is of `"TYPE_INTEGER"`.

### Metadata or ancillary information

The `:count` key holds the number of `:rows` returned and the `:metadata` key may hold information related to the property and results, and in this case it's returned the currency code of `"AUD"` (Australia) and the time zone of `"Australia/Melbourne"` (this may be different for you). You'll also see in the metadata the a value for `"subjectToThresholding"`. When this is `true`, it means the report is subject to thresholding and only returns data that meets the minimum [aggregation thresholds](https://support.google.com/analytics/answer/9383630). In this case, GA4 may be witholding data to prevent anyone viewing a report from inferring the identity or sensitive information of individual users based on demographics, interests, or other signals present in the data.

### Data of interest

The data we're interested in sits within the `:rows` key. The column heading for each item in the row is under the `:columns` key.

Note that as `%ReqGA.ReportResponse{}` implements the `Table.Response` protocol, the returned struct can be piped into `Kino.DataTable.new()`, `Explorer.DataFrame.new()` and other functions which are compatible with `Table.Response`.

*Let's try that!*

```elixir
# Visualise the report response in a Kino DataTable
country_data |> Kino.DataTable.new()
```

```elixir
# Let's put it into a DataFrame
country_data |> Explorer.DataFrame.new()
```

The type data under the `:metrics` key is used to usher the data into the correct Elixir data type. As you can see, Explorer has correctly applied the right type for each column as it is compatible with the `Table.Reader` protocol. This is true of the previous example, where Kino has also picked up the correct type information.

### Charting

Let's visulise this as a Kino chart:

<!-- livebook:{"attrs":"eyJjaGFydF90aXRsZSI6IkFjdGl2ZSB1c2VycyBieSBjb3VudHJ5IiwiaGVpZ2h0IjozNTAsImxheWVycyI6W3siYWN0aXZlIjp0cnVlLCJjaGFydF90eXBlIjoiYmFyIiwiY29sb3JfZmllbGQiOiJjb3VudHJ5IiwiY29sb3JfZmllbGRfYWdncmVnYXRlIjpudWxsLCJjb2xvcl9maWVsZF9iaW4iOm51bGwsImNvbG9yX2ZpZWxkX3NjYWxlX3NjaGVtZSI6bnVsbCwiY29sb3JfZmllbGRfdHlwZSI6Im5vbWluYWwiLCJkYXRhX3ZhcmlhYmxlIjoiY291bnRyeV9kYXRhIiwiZ2VvZGF0YV9jb2xvciI6ImJsdWUiLCJsYXRpdHVkZV9maWVsZCI6bnVsbCwibG9uZ2l0dWRlX2ZpZWxkIjpudWxsLCJ4X2ZpZWxkIjoiYWN0aXZlVXNlcnMiLCJ4X2ZpZWxkX2FnZ3JlZ2F0ZSI6bnVsbCwieF9maWVsZF9iaW4iOm51bGwsInhfZmllbGRfc2NhbGVfdHlwZSI6bnVsbCwieF9maWVsZF90eXBlIjoicXVhbnRpdGF0aXZlIiwieV9maWVsZCI6ImNvdW50cnkiLCJ5X2ZpZWxkX2FnZ3JlZ2F0ZSI6bnVsbCwieV9maWVsZF9iaW4iOm51bGwsInlfZmllbGRfc2NhbGVfdHlwZSI6bnVsbCwieV9maWVsZF90eXBlIjoibm9taW5hbCJ9XSwidmxfYWxpYXMiOiJFbGl4aXIuVmVnYUxpdGUiLCJ3aWR0aCI6NjAwfQ","chunks":null,"kind":"Elixir.KinoVegaLite.ChartCell","livebook_object":"smart_cell"} -->

```elixir
VegaLite.new(width: 600, height: 350, title: "Active users by country")
|> VegaLite.data_from_values(country_data, only: ["activeUsers", "country"])
|> VegaLite.mark(:bar)
|> VegaLite.encode_field(:x, "activeUsers", type: :quantitative)
|> VegaLite.encode_field(:y, "country", type: :nominal)
|> VegaLite.encode_field(:color, "country", type: :nominal)
```

In this example, it looks like the website is very well targetted to Australian users!

You'll no doubt get a different result and feel free to experiment with the chart accordingly.

## Get metadata for a property

Metadata allows you to gather dimensions and metrics (including custom dimensions and metrics) for the property.

In the following example, we'll see if there are any custom events configured on our property To do this we'll:

* call the `:metadata` endpoint for our property
* access the `"dimensions"` key on the response
* filter on the `"apiName"` key for dimensions starting with `"customEvent"`.

Note that the `:metadata` endpoint requires one of the following OAuth scopes:

* https://www.googleapis.com/auth/analytics.readonly
* https://www.googleapis.com/auth/analytics

*Lets take a look!*

```elixir
# List the custom dimensions added to a property
Req.get!(req, ga: :metadata, property_id: property_id).body["dimensions"]
|> Enum.filter(fn dimension -> String.starts_with?(dimension["apiName"], "customEvent") end)
```

That is a lot of custom events!

If your GA4 property has no custom events, comment out or remove the line starting with `|> Enum.filter...` and re-run the command. You'll notice that it brings back ALL metrics and dimensions on the property, including those standard ones built in to GA4.

## Get custom dimension and metrics for a property

Because there are a range of APIs for interacting with GA4, sometimes there are some slight overlaps in terms of features.

For example, the Admin API also allows you to query, add or update dimensions and metrics for a property.

The results will be slightly different than above.

The `:custom_dimensions` API call requires one of the following OAuth scopes:

* https://www.googleapis.com/auth/analytics.readonly
* https://www.googleapis.com/auth/analytics.edit

Lets query it now:

```elixir
# Enter an ID of a GA4 property in the following format:
property_id = "properties/453770299"

res = Req.get!(req, ga: :custom_dimensions, property_id: property_id)
```

Although it brings back a list of maps, it can be piped into a Kino DataTable or Explorer DataFrame *if* they have the same keys.

However, in this example only *some* dimensions have `"descriptions"`, so we'll drop these first before piping into Kino DataTable:

```elixir
res.body |> Enum.map(&Map.drop(&1, ["description"])) |> Kino.DataTable.new()
```

You should see the list of custom dimensions for that property.

## Account summaries

The Admin API has a very convient features which allows you to see what accounts you have access to and what properties they contain.

To make these calls, the Google Analytics Admin API and requires one of the following OAuth scopes:

* https://www.googleapis.com/auth/analytics.readonly
* https://www.googleapis.com/auth/analytics.edit

Let's give it a go:

```elixir
res = Req.get!(req, ga: :account_summaries)
```

If you inspect the body, you'll see some nested structs:

* `%ReqGA.AccountList{}`; with a list of:
* `%ReqGA.Account{}`; with a list of:
* `%ReqGA.Property{}`.

This structure will flatten out into a tabular format, if you pipe it into `Kino.DataTable.new()` or `Explorer.DataFrame.new()`... or even into `Table.Response.init()` if you want the raw flattened data structure.

```elixir
res.body |> Kino.DataTable.new()
```

## Single account lookup

Likewise, a single account can be looked up by passing the `:account_id`. This requires the same OAuth scopes as above.

```elixir
account_id = "accounts/324831266"
res = Req.get!(req, ga: :accounts, account_id: account_id)
```

```elixir
res.body
```

There are other single account methods such as **delete**.

The Google Analytics Admin API and requires the following OAuth scopes to delete the account:

* https://www.googleapis.com/auth/analytics.edit

And you can call Req's delete function as follows:

```
Req.delete!(req, ga: :accounts, account_id: account_id)
```

## Single property lookup

Single properties can also be looked up.

The Google Analytics Admin API requires one of the following OAuth scopes:

* https://www.googleapis.com/auth/analytics.readonly
* https://www.googleapis.com/auth/analytics.edit

```elixir
property_id = "properties/471246375"
res = Req.get!(req, ga: :properties, property_id: property_id)
```

```elixir
res.body
```

There are other single property methods such as **delete**.

The Google Analytics Admin API and requires the following OAuth scope to delete the property:

* https://www.googleapis.com/auth/analytics.edit

And you can call Req's delete function as follows:

```
property_id = "properties/471246375"
res = Req.delete!(req, ga: :properties, property_id: property_id)
```

## Adding a custom dimension

```elixir
property_id = "properties/471246375"

custom_dimension_data = %{
  "parameterName" => "page_type",
  "displayName" => "Page type",
  "description" => "The type of page, such as a Landing page, Article, Blog, etc.",
  "scope" => "EVENT"
}

res = Req.post!(req, ga: :custom_dimensions, property_id: property_id, json: custom_dimension_data)
```

## List key events for a property

```elixir
property_id = "properties/453770299"
res = Req.get!(req, ga: :key_events, property_id: property_id)
```

## List audiences for a property

```elixir
property_id = "properties/453770299"
res = Req.get!(req, ga: :audience_list, property_id: property_id)
```

*That ends our ReqGA demo!*

Hopefully this has given you a sense of how you can use `ReqGA` with `Req` and `Goth` for interacting with Google Analytics 4 APIs.

<!-- livebook:{"offset":19147,"stamp":{"token":"XCP.5aj-ecK-2PEJ0GYqkXHRAo4Tfo5TTehYyO6tP3nN4p0eSQX7Y9v-yCdn-mmHjtY4VYNnnhP33r4dt2ZUjf65X--Bxq8k8N51PIYxdA","version":2}} -->
